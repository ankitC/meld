
include #coloring/random50.meld

type linear forcecolorchange(node).
type linear neighbornotice(node, node).
type linear notcolorable(node).
type linear set(node, int).
type linear myset(node, int, int, int).
type linear changedcolor(node).
type linear color(node, int, int).
type linear edgecolor(node, node, int, int).
type linear neighborchangedcolor(node, node, int, int).
type linear usablecolors(node, list int, int, int).
type linear waitfornew(node, int, int).

prio neighborchangedcolor < set.
prio neighborchangedcolor < myset.
prio neighborchangedcolor < changedcolor.
prio neighborchangedcolor < color.

extern int randint(int).
extern list int intlistdiff(list int, list int).
extern int intlistnth(list int, int).
extern int intlistlength(list int).

const numcolors = 3.
const nocolor = 0.
const red = 1.
const blue = 2.
const green = 3.
const false = 0.
const true = 1.
const firstphase = 0.
const secondphase = 1.
const allcolors = [red, blue, green].
const maxiterations = 1000.

color(A, nocolor, 0).
forcecolorchange(A). /* this kickstarts color selection */

!edge(A, B) -o edgecolor(A, B, nocolor, 0).

forcecolorchange(A), color(A, _, Cur)
	-o color(A, randint(numcolors) + 1, Cur + 1), changedcolor(A).

changedcolor(A),
color(A, New, I)
   -o {B | !edge(A, B) | neighborchangedcolor(B, A, New, I)}, color(A, New, I).

waitfornew(A, Cur, firstphase),
neighborchangedcolor(A, B, NewColor, I),
edgecolor(A, B, OldColor, _),
color(A, nocolor, _)
   -o edgecolor(A, B, NewColor, I), myset(A, Cur, nocolor, secondphase).

/* try again */
waitfornew(A, Cur, firstphase), color(A, nocolor, _)
	-o myset(A, Cur, nocolor, secondphase).

/* looks like we failed, notify neighbor... */
waitfornew(A, Cur, secondphase),
$edgecolor(A, B, C, EIt),
!edge(A, B)
	-o set(B, EIt), neighbornotice(B, A).

neighborchangedcolor(A, B, New, I),
edgecolor(A, B, Old, _)
   -o edgecolor(A, B, New, I).

myset(A, Cur, Mine, Phase), Cur < maxiterations -o
   [collect => X | B, I | !edge(A, B), $edgecolor(A, B, X, I), X <> Mine | usablecolors(A, intlistdiff(intlistdiff(allcolors, X), [Mine]), Cur, Phase)].
myset(A, Cur, _, _), Cur >= maxiterations -o
	notcolorable(A).

set(A, Cur), color(A, Mine, Cur) -o
   [collect => X | B, I | !edge(A, B), $edgecolor(A, B, X, I), X <> Mine | usablecolors(A, intlistdiff(intlistdiff(allcolors, X), [Mine]), Cur, firstphase)].

set(A, _) -o .

usablecolors(A, [], I, Phase) -o
	changedcolor(A), color(A, nocolor, I), waitfornew(A, I, Phase).
usablecolors(A, L, I, _) -o
	changedcolor(A), color(A, intlistnth(L, randint(intlistlength(L))), I + 1).

!edge(A, B),
$edgecolor(A, B, C, EIt),
color(A, C, MyIt),
B > A,
C <> nocolor
   -o myset(A, MyIt, C, firstphase), set(B, EIt).

!edge(A, B),
neighbornotice(A, B),
$edgecolor(A, B, nocolor, EIt)
	-o set(B, EIt).
